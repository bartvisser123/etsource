# This query updates:
# - the number of corner_houses in the AREA-file
# - the useful heat demand of corner_houses based on the changed number of corner_houses
# - the insulation costs based on the changed housing stock

- query =
  future_number_of_residences = QUERY_FUTURE(-> { AREA(number_of_residences) });
  future_number_of_corner_houses = DIVIDE(USER_INPUT(),100.0) * future_number_of_residences;
  change_in_corner_houses = DIVIDE(future_number_of_corner_houses, QUERY_FUTURE(-> { AREA(number_of_corner_houses) }));

  future_useful_demand =
    IF(QUERY_PRESENT(-> { AREA(number_of_corner_houses) }) > 0.0,
    change_in_corner_houses * QUERY_FUTURE(-> { V(households_corner_houses_useful_demand_for_space_heating, preset_demand) }),
    PRODUCT(future_number_of_corner_houses, QUERY_PRESENT(-> { AREA(typical_useful_demand_space_heating_apartment) }) * (1.0 - (INPUT_VALUE(households_insulation_level_corner_houses)/100.0)))
    );

  EACH(
    UPDATE(AREA(), number_of_corner_houses, future_number_of_corner_houses),
    UPDATE(V(households_space_heating_savings_from_insulation), initial_investment, QUERY_FUTURE(-> { Q(costs_building_and_installations_households_insulation)})),
    UPDATE(V(households_corner_houses_useful_demand_for_space_heating), preset_demand, future_useful_demand)
  )
- priority = 0
- share_group = housing_stock
- max_value = 100.0
- min_value = 0.0
- start_value_gql = present:DIVIDE(AREA(number_of_corner_houses),AREA(number_of_residences)) * 100.0
- step_value = 0.1
- unit = %
- update_period = future
- update_type = mixed
